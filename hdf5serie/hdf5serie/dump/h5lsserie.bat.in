@echo off
rem Windows batch variant of h5lsserie (h5lsserie.bat.in)
rem If editing this file, edit also the file h5lsserie.in!
rem
rem Copyright (C) 2009 Markus Friedrich
rem
rem This library is free software; you can redistribute it and/or 
rem modify it under the terms of the GNU Lesser General Public 
rem License as published by the Free Software Foundation; either 
rem version 2.1 of the License, or (at your option) any later version. 
rem  
rem This library is distributed in the hope that it will be useful, 
rem but WITHOUT ANY WARRANTY; without even the implied warranty of 
rem MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU 
rem Lesser General Public License for more details. 
rem  
rem You should have received a copy of the GNU Lesser General Public 
rem License along with this library; if not, write to the Free Software 
rem Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301 USA
rem
rem Contact:
rem   mafriedrich@users.berlios.de
rem

set "H=0" rem option -h, --help: help
set "D=0" rem option -d: show description
set "L=0" rem optoin -l: show Column/Member Label
set "F=0" rem option -f: follow external links
set FILE=""
set FILEDIRNAME=""
set FILEBASENAME=""
:beginfor
if %1!==! goto continue
  if "%1"=="-d" (
    set "D=1"
    shift
    goto beginfor
  )
  if "%1"=="-h" ( 
    set "H=1"
    shift
    goto beginfor
  )
  if "%1"=="--help" (
    set "H=1"
    shift
    goto beginfor
  )
  if "%1"=="-l" (
    set "L=1"
    shift
    goto beginfor
  )
  if "%1"=="-f" (
    set "F=1"
    shift
    goto beginfor
  )
  set FILE=%1
  set FILEDIRNAME=%~dp1
  set FILEBASENAME=%~n1
  shift
goto beginfor
:continue

if not "%H%"=="1" goto afterhelp
  echo "h5lsserie"
  echo ""
  echo "Lists the groups and datasets in a hdf5 file."
  echo ""
  echo "Copyright (C) 2009 Markus Friedrich <mafriedrich@users.berlios.de>"
  echo "This is free software; see the source for copying conditions. There is NO"
  echo "warranty; not even for MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE."
  echo "Licensed under the GNU Lesser General Public License (LGPL)"
  echo ""
  echo "Usage:"
  echo "  h5lsserie [-d] [-l] <file.h5>"
  echo "    -h, --help: Show this help"
  echo "    -d:         Show 'Description' attribute"
  echo "    -l:         Show 'Column/Member Label'"
  echo "    -f:         Follow external links"
  goto end
:afterhelp

set datadir=@win_prefix@/share
set win_h5dump=@win_h5dump@
set win_xsltproc=@win_xsltproc@

rem get environment variables (for none default intallation)
if defined HDF5SERIEDATADIR set datadir=%HDF5SERIEDATADIR%
if defined HDF5SERIEH5DUMP set win_h5dump=%HDF5SERIEH5DUMP%
if defined HDF5SERIEXSLTPROC set win_xsltproc=%HDF5SERIEXSLTPROC%

rem create XML dump (without data) of FILE and all linked files (recursive)
set ALLGENERATEDFILES=
cd %FILEDIRNAME%
call:xmldump %FILE%

rem call xslt
set DIR=%FILEDIRNAME%
%win_xsltproc% --path %DIR% --param DIR "'%DIR%'" --param FILENAME "'%FILE%'" --param LABEL %L% --param DESCRIPTION %D% --param FOLLOW %F% %datadir%/hdf5serie/xsl/h5lsserie.xsl .%FILEBASENAME%.h5.xml

rem delete all previous created .*.h5.xml files
for %%S in (%ALLGENERATEDFILES%) do (
  del %%S
)

:end
goto:eof


:: Start Functions
:xmldump
set ALLGENERATEDFILES=%ALLGENERATEDFILES% .%~1.xml
set OUT=%~dp1/.%~n1.xml
%win_h5dump% -xA %~1 > %OUT%
if not "%F%" == "1" goto endxmldump
  set MYVAR=
  for /f "tokens=*" %%A in ('%win_xsltproc% --path . %datadir%/hdf5serie/xsl/h5lsserie-listlinks.xsl %OUT%') do set MYVAR=%%A
  for %%S in (%MYVAR%) do (
    call:xmldump %~dp1/%%S
  )
:endxmldump
goto:eof
